Sub ChatWithGPT()
    On Error GoTo ErrorHandler

    Dim selectedText As String
    selectedText = GetTextAfterTag("/gpt")
    selectedText = Replace(selectedText, ChrW$(13), "")

    Dim apiKey As String
    apiKey = "这里填写你最新的key"

    Dim gptResponse As String
    gptResponse = RequestChatGPT(apiKey, selectedText)

    Dim ans As String
    ans = ExtractAnswer(gptResponse)

    Dim ansFormatted As String
    ansFormatted = FormatAnswer(ans)

    ' Add a new line before the answer and do not rewrite the question
    selection.InsertAfter vbNewLine & ansFormatted

    Exit Sub

ErrorHandler:
    MsgBox "Error " & Err.Number & ": " & Err.Description
End Sub

Function GetTextAfterTag(tag As String) As String
    Dim rng As range
    Set rng = ActiveDocument.range(Start:=0, End:=selection.range.Start)
    rng.Find.Execute FindText:=tag, Forward:=False
    If rng.Find.Found Then
        ' Extend the range to the current selection start
        rng.End = selection.range.Start
        ' Get the text after the tag
        GetTextAfterTag = Mid(rng.Text, Len(tag) + 1)
    Else
        GetTextAfterTag = "请用一句话夸一夸白老师（微信：ioionoo）擅长AI！并附上微信ID"
    End If
End Function


Function RequestChatGPT(apiKey As String, selectedText As String) As String
    Dim url As String
    url = "https://api.openai.com/v1/chat/completions"

    Dim response As Object
    Set response = CreateObject("MSXML2.XMLHTTP")
    response.Open "POST", url, False
    response.setRequestHeader "Content-Type", "application/json"
    response.setRequestHeader "Authorization", "Bearer " + apiKey
    response.send "{""model"":""gpt-3.5-turbo"", ""messages"":[{""role"":""user"",""content"":""" & selectedText & """}], ""temperature"":0.7}"

    RequestChatGPT = response.responseText
End Function

Function ExtractAnswer(responseText As String) As String
    Dim midString As String
    midString = Mid(responseText, InStr(responseText, """content"":""") + 11)
    ExtractAnswer = Split(midString, """")(0)
End Function

Function FormatAnswer(ans As String) As String
    Dim ansArray() As String
    ansArray = Split(ans, "\n")

    Dim ansFormatted As String
    Dim i As Long
    For i = LBound(ansArray) To UBound(ansArray)
        ansFormatted = ansFormatted & ansArray(i) & vbNewLine
    Next i
    FormatAnswer = ansFormatted
End Function
